#autoload

_autocomplete.recent_paths() {
  local -i nmatches=$compstate[nmatches]
  (( nmatches > 0 )) &&
    return 1

  local -aU reply=()
  _tags recent-directories recent-files
  while _tags; do

    _requested recent-directories && [[ -v functions[_autocomplete.recent_dirs] ]] &&
        _autocomplete.recent_dirs $words[CURRENT] &&
      .autocomplete.recent_paths.add recent-directories 'recent directory' $reply

    _requested recent-files && [[ -v functions[_autocomplete.recent_files] ]] &&
        _autocomplete.recent_files $words[CURRENT] &&
      .autocomplete.recent_paths.add recent-files 'recent file' $reply
  done

  (( compstate[nmatches] > nmatches ))
}

.autocomplete.recent_paths.add() {
  local expl
  _description -V $1 expl $2
  shift 2

  local -a display=()
  local -i max_matches=$(( compstate[nmatches] + 10 ))
  local disp path prefix word=$~words[CURRENT]:a
  for path in ${@:a}; do
    (( compstate[nmatches] < max_matches )) ||
      break

    [[ -e $path ]] ||
      continue

    disp=${(D)path}
    (( $#disp > 1 )) ||
      continue

    prefix=$path:h
    [[ $word == ($path|$prefix) || $prefix == ($word:h|$PWD) ]] &&
      continue

    prefix+=${${prefix:#/}:+/}
    display=( "$disp" )
    compadd "$expl[@]" -fQ -P ${(q)prefix} -W $prefix -d display - ${(q)path:t}
  done
}

_autocomplete.recent_paths "$@"
